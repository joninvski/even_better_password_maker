buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath parent.ext.libraries.androidtools_gradle
        classpath parent.ext.libraries.spoon_gradle
        classpath parent.ext.libraries.sdkmanager_gradle
        classpath parent.ext.libraries.android_cq_gradle
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'android-cq'


repositories {
    jcenter()
    maven { url 'http://dl.bintray.com/joninvski/android-libs' }
}

android {

    compileSdkVersion parent.ext.compileSdkVersion
    buildToolsVersion parent.ext.buildToolsVersion

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        versionCode parent.ext.versionCode
        versionName parent.ext.versionName

        minSdkVersion parent.ext.minSdkVersion
        targetSdkVersion parent.ext.targetSdkVersion
    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            assert localProps['keystore.props.file'];
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFile 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile parent.ext.libraries.chromahashview
    compile parent.ext.libraries.supportv4
    compile parent.ext.libraries.appcompat
    compile parent.ext.libraries.butterknife
    compile parent.ext.libraries.timber
    compile project(':domain')
    androidTestCompile parent.ext.libraries.spoonclient
    androidTestCompile parent.ext.libraries.robotium
    androidTestCompile parent.ext.libraries.junit
}

